apply plugin: "java"
apply plugin: "idea"
apply plugin: "maven"
apply plugin: "signing"
apply plugin: "checkstyle"
apply plugin: "findbugs"
apply plugin: "pmd"

compileJava {
    sourceCompatibility = 1.8
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "com.github.bensmith87"
archivesBaseName = "ui"
version = "0.1-SNAPSHOT"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'UI'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'OpenGL UI Library'
                    url 'https://github.com/bensmith87/ui'

                    scm {
                        connection 'scm:git:git@github.com:bensmith87/ui.git'
                        developerConnection 'scm:git@github.com:bensmith87/ui.git'
                        url 'https://github.com/bensmith87/ui'
                    }

                licenses {
                    license {
                        name 'GNU Lesser General Public License, Version 3'
                        url 'http://www.gnu.org/licenses/lgpl-3.0.en.html'
                    }
                }

                developers {
                    developer {
                        id 'bensmith87'
                        name 'Ben Smith'
                        email 'bensmith87@gmail.com'
                    }
                }
            }
        }
    }
}

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    compile "log4j:log4j:1.2.17"
    compile "com.google.code.findbugs:annotations:3.0.1"
    compile "org.jogamp.gluegen:gluegen-rt-main:2.3.2"
    compile "org.jogamp.jogl:jogl-all-main:2.3.2"

    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "junit:junit-dep:4.11"
    testCompile "org.hamcrest:hamcrest-library:1.3"
    testCompile "org.mockito:mockito-core:1.9.+"
}

checkstyle {
    toolVersion = "6.3"
    checkstyleTest.enabled = false
}

pmd {
    consoleOutput = true
}
